# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//chrome/android/features/cablev2_authenticator/config.gni")

if (enable_android_cablev2_authenticator) {
  android_library("internal_java") {
    sources = [
      "java/src/org/chromium/chrome/browser/webauth/authenticator/BLEHandler.java",
      "java/src/org/chromium/chrome/browser/webauth/authenticator/CableAuthenticator.java",
      "java/src/org/chromium/chrome/browser/webauth/authenticator/CableAuthenticatorUI.java",
      "java/src/org/chromium/chrome/browser/webauth/authenticator/CameraView.java",
      "java/src/org/chromium/chrome/browser/webauth/authenticator/QRScanDialog.java",
    ]

    deps = [
      "$google_play_services_package:google_play_services_base_java",
      "$google_play_services_package:google_play_services_fido_java",
      "$google_play_services_package:google_play_services_tasks_java",
      "$google_play_services_package:google_play_services_vision_common_java",
      "$google_play_services_package:google_play_services_vision_java",
      "//base:base_java",
      "//base:jni_java",
      "//chrome/android:chrome_java",
      "//content/public/android:content_java",
      "//third_party/android_deps:android_support_v7_appcompat_java",
      "//third_party/android_deps:androidx_annotation_annotation_java",
      "//ui/android:ui_full_java",
    ]

    annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
  }
} else {
  java_group("internal_java") {
    deps = [
      # This is needed to prevent a build-error. See comment in
      # https://chromium-review.googlesource.com/c/chromium/src/+/2079613/12/chrome/android/features/cablev2_authenticator/internal/BUILD.gn#9
      "//third_party/android_deps:androidx_annotation_annotation_java",
    ]
  }
}

generate_jni("jni_headers") {
  sources = [ "java/src/org/chromium/chrome/browser/webauth/authenticator/BLEHandler.java" ]
}

# Native code cannot change between different release channels and so this is
# always enabled.
source_set("native") {
  sources = [ "native/cablev2_authenticator_android.cc" ]

  deps = [
    ":jni_headers",
    "//base",
    "//components/cbor",
    "//components/device_event_log",
    "//content/public/browser",
    "//crypto",
    "//device/fido",
    "//third_party/boringssl",
  ]
}
