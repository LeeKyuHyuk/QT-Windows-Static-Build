# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/polymer/polymer.gni")
import("../optimize_webui.gni")

if (optimize_webui) {
  history_pak_file = "history_resources.pak"
  unpak_folder = "history_resources.unpak"

  optimize_webui("build") {
    host = "history"
    js_module_in_files = [
      "history.js",
      "lazy_load.js",
    ]
    input = rebase_path("$target_gen_dir/$unpak_folder", root_build_dir)
    js_out_files = [
      "history.rollup.js",
      "lazy_load.rollup.js",
      "shared.rollup.js",
    ]

    deps = [
      ":unpak",
      "../../../../ui/webui/resources:modulize",
    ]
    excludes = [ "chrome://resources/js/cr.m.js" ]
  }

  unpak("unpak") {
    pak_file = history_pak_file
    out_folder = unpak_folder

    deps = [ ":flattened_resources" ]
  }

  grit("flattened_resources") {
    source = "history_resources.grd"

    grit_flags = [
      "-E",
      "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    ]

    deps = [ ":polymer3_elements" ]
    defines = chrome_grit_defines
    outputs = [
      "grit/history_resources.h",
      "grit/history_resources_map.cc",
      "grit/history_resources_map.h",
      history_pak_file,
    ]
    output_dir = "$root_gen_dir/chrome/browser/resources/history"
  }
}

js_type_check("closure_compile") {
  is_polymer3 = true
  deps = [
    ":app",
    ":browser_service",
    ":constants",
    ":externs",
    ":history_item",
    ":history_list",
    ":history_toolbar",
    ":query_manager",
    ":router",
    ":searched_label",
    ":side_bar",
    ":synced_device_card",
    ":synced_device_manager",
  ]
}

js_library("constants") {
}

js_library("browser_service") {
  deps = [
    ":constants",
    ":externs",
    "//ui/webui/resources/js:cr.m",
  ]
}

js_library("externs") {
}

js_library("history_item") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:icon.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_row_behavior.m",
  ]
}

js_library("history_list") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    ":history_item",
    "//third_party/polymer/v3_0/components-chromium/iron-a11y-announcer:iron-a11y-announcer",
    "//third_party/polymer/v3_0/components-chromium/iron-list:iron-list",
    "//third_party/polymer/v3_0/components-chromium/iron-scroll-threshold:iron-scroll-threshold",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
}

js_library("history_toolbar") {
  deps = [
    ":constants",
    ":externs",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar.m",
    "//ui/webui/resources/cr_elements/cr_toolbar:cr_toolbar_selection_overlay.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("app") {
  deps = [
    ":constants",
    ":externs",
    ":history_list",
    ":history_toolbar",
    ":side_bar",
    ":synced_device_card",
    ":synced_device_manager",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_drawer:cr_drawer.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:find_shortcut_behavior.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
}

js_library("query_manager") {
  deps = [
    ":browser_service",
    ":externs",
    ":router",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("router") {
  deps = [
    ":constants",
    ":externs",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
  ]
}

js_library("searched_label") {
  deps = [
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("side_bar") {
  deps = [
    ":browser_service",
    "//third_party/polymer/v3_0/components-chromium/iron-a11y-keys-behavior:iron-a11y-keys-behavior",
    "//third_party/polymer/v3_0/components-chromium/iron-selector:iron-selector",
    "//third_party/polymer/v3_0/components-chromium/paper-ripple:paper-ripple",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
  ]
}

js_library("synced_device_card") {
  deps = [
    ":browser_service",
    ":constants",
    ":externs",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:icon.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_row.m",
  ]
}

js_library("synced_device_manager") {
  deps = [
    ":browser_service",
    ":synced_device_card",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_grid.m",
  ]
}

group("polymer3_elements") {
  public_deps = [
    ":app_module",
    ":history_item_module",
    ":history_list_module",
    ":history_toolbar_module",
    ":router_module",
    ":shared_style_module",
    ":shared_vars_module",
    ":side_bar_module",
    ":synced_device_card_module",
    ":synced_device_manager_module",
  ]
}

polymer_modulizer("app") {
  html_file = "app.html"
  js_file = "app.js"
  html_type = "v3-ready"
}

polymer_modulizer("history_item") {
  html_file = "history_item.html"
  js_file = "history_item.js"
  html_type = "v3-ready"
}

polymer_modulizer("history_list") {
  html_file = "history_list.html"
  js_file = "history_list.js"
  html_type = "v3-ready"
}

polymer_modulizer("history_toolbar") {
  html_file = "history_toolbar.html"
  js_file = "history_toolbar.js"
  html_type = "v3-ready"
}

polymer_modulizer("router") {
  html_file = "router.html"
  js_file = "router.js"
  html_type = "v3-ready"
}

polymer_modulizer("shared_style") {
  html_file = "shared_style.html"
  js_file = "shared_style.js"
  html_type = "v3-ready"
}

polymer_modulizer("shared_vars") {
  html_file = "shared_vars.html"
  js_file = "shared_vars.js"
  html_type = "v3-ready"
}

polymer_modulizer("side_bar") {
  html_file = "side_bar.html"
  js_file = "side_bar.js"
  html_type = "v3-ready"
}

polymer_modulizer("synced_device_card") {
  html_file = "synced_device_card.html"
  js_file = "synced_device_card.js"
  html_type = "v3-ready"
}

polymer_modulizer("synced_device_manager") {
  html_file = "synced_device_manager.html"
  js_file = "synced_device_manager.js"
  html_type = "v3-ready"
}
