# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":a11y_page",
    ":captions_browser_proxy",
    ":captions_subpage",
  ]
}

js_library("captions_subpage") {
  deps = [
    "../appearance_page:fonts_browser_proxy",
    "../controls:settings_dropdown_menu",
    "../prefs:prefs_behavior",
    "//ui/webui/resources/cr_elements/cr_slider:cr_slider",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("a11y_page") {
  deps = [
    "..:route",
    "..:router",
    "../settings_page:settings_animated_pages",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("captions_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr" ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":a11y_page.m",
    ":captions_browser_proxy.m",
    ":captions_subpage.m",
  ]
}

js_library("a11y_page.m") {
  sources = [
    "$root_gen_dir/chrome/browser/resources/settings/a11y_page/a11y_page.m.js",
  ]
  deps = [
    ":captions_browser_proxy.m",
    "..:route.m",
    "..:router.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":a11y_page_module" ]
}

js_library("captions_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/a11y_page/captions_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("captions_subpage.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/a11y_page/captions_subpage.m.js" ]
  deps = [
    "../appearance_page:fonts_browser_proxy.m",
    "../controls:settings_dropdown_menu.m",
    "../prefs:prefs_behavior.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":captions_subpage_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":a11y_page_module",
    ":captions_subpage_module",
    ":modulize",
  ]
}

polymer_modulizer("a11y_page") {
  js_file = "a11y_page.js"
  html_file = "a11y_page.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/a11y_page/captions_browser_proxy.html|CaptionsBrowserProxyImpl",
                   "chrome/browser/resources/settings/route.html|routes",
                   "chrome/browser/resources/settings/router.html|Router",
                 ]
  namespace_rewrites = settings_namespace_rewrites
}

polymer_modulizer("captions_subpage") {
  js_file = "captions_subpage.js"
  html_file = "captions_subpage.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/a11y_page/captions_browser_proxy.html|CaptionsBrowserProxy",
                   "chrome/browser/resources/settings/controls/settings_dropdown_menu.html|DropdownMenuOptionList",
                   "chrome/browser/resources/settings/appearance_page/fonts_browser_proxy.html|FontsBrowserProxy, FontsBrowserProxyImpl, FontsData",
                 ]
  namespace_rewrites = settings_namespace_rewrites
}

js_modulizer("modulize") {
  input_files = [ "captions_browser_proxy.js" ]
  namespace_rewrites = settings_namespace_rewrites
}
