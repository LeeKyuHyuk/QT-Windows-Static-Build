# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":on_startup_browser_proxy",
    ":on_startup_page",
    ":startup_url_dialog",
    ":startup_url_entry",
    ":startup_urls_page",
    ":startup_urls_page_browser_proxy",
  ]
}

js_library("on_startup_browser_proxy") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
  ]
}

js_library("on_startup_page") {
  deps = [
    ":on_startup_browser_proxy",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("startup_urls_page") {
  deps = [
    ":startup_url_entry",
    ":startup_urls_page_browser_proxy",
    "//ui/webui/resources/cr_elements:cr_scrollable_behavior",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:web_ui_listener_behavior",
    "//ui/webui/resources/js/cr/ui:focus_without_ink",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
}

js_library("startup_urls_page_browser_proxy") {
  deps = [
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_library("startup_url_entry") {
  deps = [
    ":startup_urls_page_browser_proxy",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:icon",
    "//ui/webui/resources/js/cr/ui:focus_row_behavior",
  ]
}

js_library("startup_url_dialog") {
  deps = [
    ":startup_urls_page_browser_proxy",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:load_time_data",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":on_startup_browser_proxy.m",
    ":on_startup_page.m",
    ":startup_url_dialog.m",
    ":startup_url_entry.m",
    ":startup_urls_page.m",
    ":startup_urls_page_browser_proxy.m",
  ]
}

js_library("on_startup_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/on_startup_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("on_startup_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/on_startup_page.m.js" ]
  deps = [
    ":on_startup_browser_proxy.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":on_startup_page_module" ]
}

js_library("startup_url_dialog.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/startup_url_dialog.m.js" ]
  deps = [
    ":startup_urls_page_browser_proxy.m",
    "..:i18n_setup.m",
    "//ui/webui/resources/cr_elements/cr_dialog:cr_dialog.m",
  ]
  extra_deps = [ ":startup_url_dialog_module" ]
}

js_library("startup_url_entry.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/startup_url_entry.m.js" ]
  deps = [
    ":startup_urls_page_browser_proxy.m",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:icon.m",
    "//ui/webui/resources/js/cr/ui:focus_row_behavior.m",
  ]
  extra_deps = [ ":startup_url_entry_module" ]
}

js_library("startup_urls_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/startup_urls_page.m.js" ]
  deps = [
    ":startup_url_entry.m",
    ":startup_urls_page_browser_proxy.m",
    "//ui/webui/resources/cr_elements:cr_scrollable_behavior.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
    "//ui/webui/resources/js/cr/ui:focus_without_ink.m",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":startup_urls_page_module" ]
}

js_library("startup_urls_page_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/on_startup_page/startup_urls_page_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  extra_deps = [ ":modulize" ]
}

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":on_startup_page_module",
    ":startup_url_dialog_module",
    ":startup_url_entry_module",
    ":startup_urls_page_module",
  ]
}

polymer_modulizer("on_startup_page") {
  js_file = "on_startup_page.js"
  html_file = "on_startup_page.html"
  html_type = "dom-module"
  auto_imports = [ "chrome/browser/resources/settings/on_startup_page/on_startup_browser_proxy.html|NtpExtension, OnStartupBrowserProxyImpl" ]
  namespace_rewrites = settings_namespace_rewrites
}

polymer_modulizer("startup_url_dialog") {
  js_file = "startup_url_dialog.js"
  html_file = "startup_url_dialog.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [ "chrome/browser/resources/settings/on_startup_page/startup_urls_page_browser_proxy.html|StartupPageInfo, StartupUrlsPageBrowserProxy, StartupUrlsPageBrowserProxyImpl" ]
  namespace_rewrites = settings_namespace_rewrites
}

polymer_modulizer("startup_url_entry") {
  js_file = "startup_url_entry.js"
  html_file = "startup_url_entry.html"
  html_type = "dom-module"
  auto_imports = [
    "ui/webui/resources/html/assert.html|assert",
    "chrome/browser/resources/settings/on_startup_page/startup_urls_page_browser_proxy.html|StartupPageInfo, StartupUrlsPageBrowserProxyImpl",
  ]
  namespace_rewrites = settings_namespace_rewrites
}

polymer_modulizer("startup_urls_page") {
  js_file = "startup_urls_page.js"
  html_file = "startup_urls_page.html"
  html_type = "dom-module"
  auto_imports = [
    "ui/webui/resources/html/assert.html|assert",
    "chrome/browser/resources/settings/on_startup_page/startup_url_entry.html|EDIT_STARTUP_URL_EVENT",
    "chrome/browser/resources/settings/on_startup_page/startup_urls_page_browser_proxy.html|StartupPageInfo, StartupUrlsPageBrowserProxy, StartupUrlsPageBrowserProxyImpl",
  ]
  namespace_rewrites = settings_namespace_rewrites
}

js_modulizer("modulize") {
  input_files = [
    "on_startup_browser_proxy.js",
    "startup_urls_page_browser_proxy.js",
  ]
  namespace_rewrites = settings_namespace_rewrites
}
