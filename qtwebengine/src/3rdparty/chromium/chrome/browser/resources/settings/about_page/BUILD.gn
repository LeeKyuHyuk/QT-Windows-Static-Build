# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":about_page",
    ":about_page_browser_proxy",
  ]
}

js_library("about_page") {
  deps = [
    ":about_page_browser_proxy",
    "..:lifetime_browser_proxy",
    "..:router",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:parse_html_subset",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
}

js_library("about_page_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr" ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":about_page.m",
    ":about_page_browser_proxy.m",
  ]
}

js_library("about_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/about_page/about_page.m.js" ]
  deps = [
    ":about_page_browser_proxy.m",
    "..:lifetime_browser_proxy.m",
    "..:router.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:parse_html_subset.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":about_page_module" ]
}

js_library("about_page_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/about_page/about_page_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  externs_list = [ "$externs_path/chrome_send.js" ]
  extra_deps = [ ":modulize" ]
}

group("polymer3_elements") {
  public_deps = [
    ":about_page_module",
    ":modulize",
  ]
}

polymer_modulizer("about_page") {
  js_file = "about_page.js"
  html_file = "about_page.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "ui/webui/resources/html/assert.html|assert",
                   "chrome/browser/resources/settings/router.html|Router",
                   "chrome/browser/resources/settings/lifetime_browser_proxy.html|LifetimeBrowserProxy, LifetimeBrowserProxyImpl",
                   "chrome/browser/resources/settings/about_page/about_page_browser_proxy.html|AboutPageBrowserProxy, AboutPageBrowserProxyImpl, UpdateStatus, UpdateStatusChangedEvent, PromoteUpdaterStatus",
                 ]
  namespace_rewrites = settings_namespace_rewrites
}

js_modulizer("modulize") {
  input_files = [ "about_page_browser_proxy.js" ]
  namespace_rewrites = settings_namespace_rewrites
}
