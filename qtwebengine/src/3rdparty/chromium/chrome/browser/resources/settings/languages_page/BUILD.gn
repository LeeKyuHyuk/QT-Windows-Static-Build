# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":add_languages_dialog",
    ":edit_dictionary_page",
    ":languages",
    ":languages_browser_proxy",
    ":languages_page",
    ":languages_types",
  ]
}

js_library("edit_dictionary_page") {
  deps = [
    ":languages_browser_proxy",
    "..:global_scroll_target_behavior",
    "..:route",
    "../prefs",
    "//ui/webui/resources/cr_elements/cr_button:cr_button",
    "//ui/webui/resources/cr_elements/cr_input:cr_input",
  ]
}

js_library("languages_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr" ]
  externs_list = [
    "$externs_path/chrome_send.js",
    "$externs_path/input_method_private.js",
    "$externs_path/language_settings_private.js",
  ]
  extra_sources = [
    "$interfaces_path/input_method_private_interface.js",
    "$interfaces_path/language_settings_private_interface.js",
  ]
}

js_library("languages") {
  deps = [
    ":languages_browser_proxy",
    ":languages_types",
    "../prefs",
    "../prefs:prefs_types",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:promise_resolver",
  ]
  externs_list = [
    "$externs_path/input_method_private.js",
    "$externs_path/language_settings_private.js",
  ]
  extra_sources = [
    "$interfaces_path/input_method_private_interface.js",
    "$interfaces_path/language_settings_private_interface.js",
  ]
}

js_library("languages_page") {
  deps = [
    ":languages",
    ":languages_types",
    "..:lifetime_browser_proxy",
    "..:route",
    "..:router",
    "../settings_page:settings_animated_pages",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu",
    "//ui/webui/resources/cr_elements/cr_expand_button:cr_expand_button",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:load_time_data",
    "//ui/webui/resources/js/cr/ui:focus_without_ink",
  ]
}

js_library("languages_types") {
  deps = []
  externs_list = [ "$externs_path/language_settings_private.js" ]
}

js_library("add_languages_dialog") {
  deps = [
    ":languages",
    ":languages_types",
    "//ui/webui/resources/cr_elements:cr_scrollable_behavior",
    "//ui/webui/resources/cr_elements/cr_search_field:cr_search_field",
    "//ui/webui/resources/js:find_shortcut_behavior",
  ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":add_languages_dialog.m",
    ":edit_dictionary_page.m",
    ":languages.m",
    ":languages_browser_proxy.m",
    ":languages_page.m",
    ":languages_types.m",
  ]
}

js_library("add_languages_dialog.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/add_languages_dialog.m.js" ]
  deps = [
    ":languages.m",
    ":languages_types.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements:cr_scrollable_behavior.m",
    "//ui/webui/resources/cr_elements/cr_search_field:cr_search_field.m",
    "//ui/webui/resources/js:find_shortcut_behavior.m",
  ]
  extra_deps = [ ":add_languages_dialog_module" ]
}

js_library("edit_dictionary_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/edit_dictionary_page.m.js" ]
  deps = [
    ":languages_browser_proxy.m",
    "..:global_scroll_target_behavior.m",
    "..:route.m",
    "../prefs:prefs.m",
    "//third_party/polymer/v3_0/components-chromium/iron-list:iron-list",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_button:cr_button.m",
    "//ui/webui/resources/cr_elements/cr_input:cr_input.m",
  ]
  extra_deps = [ ":edit_dictionary_page_module" ]
}

js_library("languages.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/languages.m.js" ]
  deps = [
    ":languages_browser_proxy.m",
    ":languages_types.m",
    "../prefs:prefs.m",
    "../prefs:prefs_behavior.m",
    "../prefs:prefs_types.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:promise_resolver.m",
  ]
  externs_list = [
    "$externs_path/input_method_private.js",
    "$externs_path/language_settings_private.js",
  ]
  extra_sources = [
    "$interfaces_path/input_method_private_interface.js",
    "$interfaces_path/language_settings_private_interface.js",
  ]
  extra_deps = [ ":languages_module" ]
}

js_library("languages_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/languages_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  externs_list = [
    "$externs_path/chrome_send.js",
    "$externs_path/input_method_private.js",
    "$externs_path/language_settings_private.js",
  ]
  extra_sources = [
    "$interfaces_path/input_method_private_interface.js",
    "$interfaces_path/language_settings_private_interface.js",
  ]
  extra_deps = [ ":modulize" ]
}

js_library("languages_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/languages_page.m.js" ]
  deps = [
    ":languages.m",
    ":languages_types.m",
    "..:lifetime_browser_proxy.m",
    "..:route.m",
    "..:router.m",
    "../settings_page:settings_animated_pages.m",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/cr_elements/cr_action_menu:cr_action_menu.m",
    "//ui/webui/resources/cr_elements/cr_expand_button:cr_expand_button.m",
    "//ui/webui/resources/cr_elements/cr_lazy_render:cr_lazy_render.m",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
    "//ui/webui/resources/js/cr/ui:focus_without_ink.m",
  ]
  extra_deps = [ ":languages_page_module" ]
}

js_library("languages_types.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/languages_page/languages_types.m.js" ]
  externs_list = [ "$externs_path/language_settings_private.js" ]
  extra_deps = [ ":modulize" ]
}

group("polymer3_elements") {
  public_deps = [
    ":add_languages_dialog_module",
    ":edit_dictionary_page_module",
    ":languages_module",
    ":languages_page_module",
    ":modulize",
  ]
}

polymer_modulizer("add_languages_dialog") {
  js_file = "add_languages_dialog.js"
  html_file = "add_languages_dialog.html"
  html_type = "dom-module"
}

polymer_modulizer("edit_dictionary_page") {
  js_file = "edit_dictionary_page.js"
  html_file = "edit_dictionary_page.html"
  html_type = "dom-module"
  namespace_rewrites = settings_namespace_rewrites
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/languages_page/languages_browser_proxy.html|LanguagesBrowserProxyImpl",
                   "chrome/browser/resources/settings/route.html|routes",
                   "ui/webui/resources/html/polymer.html|Polymer,html,flush",
                 ]
}

polymer_modulizer("languages") {
  js_file = "languages.js"
  html_file = "languages.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/languages_page/languages_browser_proxy.html|LanguagesBrowserProxy,LanguagesBrowserProxyImpl",
                   "ui/webui/resources/html/assert.html|assert",
                   "ui/webui/resources/html/cr.html|isChromeOS, isWindows",
                 ]
  namespace_rewrites = settings_namespace_rewrites
}

polymer_modulizer("languages_page") {
  js_file = "languages_page.js"
  html_file = "languages_page.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/lifetime_browser_proxy.html|LifetimeBrowserProxyImpl",
                   "chrome/browser/resources/settings/route.html|routes",
                   "chrome/browser/resources/settings/router.html|Route,Router",
                   "ui/webui/resources/html/assert.html|assert",
                   "ui/webui/resources/html/cr.html|isChromeOS, isWindows",
                   "ui/webui/resources/html/polymer.html|Polymer,html,flush",
                 ]
  namespace_rewrites = settings_namespace_rewrites +
                       [ "settings.kMenuCloseDelay|kMenuCloseDelay" ]
}

js_modulizer("modulize") {
  input_files = [
    "languages_browser_proxy.js",
    "languages_types.js",
  ]
  namespace_rewrites = settings_namespace_rewrites
}
