# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

js_type_check("closure_compile") {
  deps = [
    ":safety_check_browser_proxy",
    ":safety_check_page",
    "..:lifetime_browser_proxy",
    "..:metrics_browser_proxy",
    "../autofill_page:password_manager_proxy",
  ]
}

js_library("safety_check_page") {
  deps = [
    ":safety_check_browser_proxy",
    "..:hats_browser_proxy",
    "..:lifetime_browser_proxy",
    "..:metrics_browser_proxy",
    "..:open_window_proxy",
    "..:route",
    "..:router",
    "../autofill_page:password_manager_proxy",
    "//third_party/polymer/v1_0/components-chromium/iron-a11y-announcer:iron-a11y-announcer-extracted",
    "//ui/webui/resources/js:assert",
    "//ui/webui/resources/js:i18n_behavior",
    "//ui/webui/resources/js:web_ui_listener_behavior",
  ]
  externs_list = [ "$externs_path/chrome_send.js" ]
}

js_library("safety_check_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr" ]
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":safety_check_browser_proxy.m",
    ":safety_check_page.m",
  ]
}

js_library("safety_check_browser_proxy.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/safety_check_page/safety_check_browser_proxy.m.js" ]
  deps = [ "//ui/webui/resources/js:cr.m" ]
  extra_deps = [ ":modulize" ]
}

js_library("safety_check_page.m") {
  sources = [ "$root_gen_dir/chrome/browser/resources/settings/safety_check_page/safety_check_page.m.js" ]
  deps = [
    ":safety_check_browser_proxy.m",
    "..:hats_browser_proxy.m",
    "..:lifetime_browser_proxy.m",
    "..:metrics_browser_proxy.m",
    "..:open_window_proxy.m",
    "..:route.m",
    "..:router.m",
    "../autofill_page:password_manager_proxy.m",
    "//third_party/polymer/v3_0/components-chromium/iron-a11y-announcer:iron-a11y-announcer",
    "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:i18n_behavior.m",
    "//ui/webui/resources/js:web_ui_listener_behavior.m",
  ]
  extra_deps = [ ":safety_check_page_module" ]
}

group("polymer3_elements") {
  public_deps = [
    ":modulize",
    ":safety_check_page_module",
  ]
}

polymer_modulizer("safety_check_page") {
  js_file = "safety_check_page.js"
  html_file = "safety_check_page.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [
                   "chrome/browser/resources/settings/route.html|routes",
                   "chrome/browser/resources/settings/router.html|Router",
                   "chrome/browser/resources/settings/autofill_page/password_manager_proxy.html|PasswordManagerImpl,PasswordManagerProxy",
                   "chrome/browser/resources/settings/lifetime_browser_proxy.html|LifetimeBrowserProxy,LifetimeBrowserProxyImpl",
                   "chrome/browser/resources/settings/hats_browser_proxy.html|HatsBrowserProxyImpl",
                   "chrome/browser/resources/settings/metrics_browser_proxy.html|SafetyCheckInteractions,MetricsBrowserProxy,MetricsBrowserProxyImpl",
                   "chrome/browser/resources/settings/open_window_proxy.html|OpenWindowProxyImpl",
                   "chrome/browser/resources/settings/safety_check_page/safety_check_browser_proxy.html|SafetyCheckBrowserProxy,SafetyCheckBrowserProxyImpl,SafetyCheckExtensionsStatus,SafetyCheckPasswordsStatus,SafetyCheckUpdatesStatus,SafetyCheckSafeBrowsingStatus,SafetyCheckCallbackConstants",
                   "ui/webui/resources/html/assert.html|assertNotReached",
                   "ui/webui/resources/html/polymer.html|Polymer,html,flush",
                 ]
  namespace_rewrites = settings_namespace_rewrites
}

js_modulizer("modulize") {
  input_files = [ "safety_check_browser_proxy.js" ]
  namespace_rewrites = settings_namespace_rewrites
}
