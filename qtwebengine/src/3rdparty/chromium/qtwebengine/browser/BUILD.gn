import("//chrome/common/features.gni")
import("//components/spellcheck/spellcheck_build_features.gni")
import("//tools/grit/grit_rule.gni")
import ("//mojo/public/tools/bindings/mojom.gni")

mojom("interfaces") {
  sources = [
    "qtwebchannel.mojom",
  ]
}

grit("qt_webengine_resources") {
  source = "qt_webengine_resources.grd"
  defines = chrome_grit_defines
  outputs = [
    "grit/qt_webengine_resources.h",
    "qt_webengine_resources.pak",
  ]
  output_dir = "$root_gen_dir/qtwebengine"
}

source_set("qtwebengine_content_browser_overlay_manifest") {
  sources = [
    "//qtwebengine/browser/qtwebengine_content_browser_overlay_manifest.cc",
    "//qtwebengine/browser/qtwebengine_content_browser_overlay_manifest.h",
  ]

  deps = [
    "//base",
    "//extensions/buildflags",
    "//services/service_manager/public/cpp",
  ]
  if (enable_extensions) {
    deps += [
      "//extensions/common:mojom",
      "//extensions/common/api:mojom",
    ]
  }
}

source_set("qtwebengine_content_renderer_overlay_manifest") {
  sources = [
    "//qtwebengine/browser/qtwebengine_content_renderer_overlay_manifest.cc",
    "//qtwebengine/browser/qtwebengine_content_renderer_overlay_manifest.h",
  ]

  deps = [
    "//base",
    "//extensions/buildflags",
    "//services/service_manager/public/cpp",
  ]
  if (enable_extensions) {
    deps += [ "//extensions/common:mojom" ]
  }
}

source_set("qtwebengine_packaged_service_manifests") {
  sources = [
    "qtwebengine_packaged_service_manifest.cc",
    "qtwebengine_packaged_service_manifest.h",
  ]

  deps = [
    "//base",
    "//components/spellcheck:buildflags",
    "//services/service_manager/public/cpp",
  ]
  if (enable_spellcheck) {
    deps += [ "//components/spellcheck/common:interfaces" ]
  }
}

source_set("qtwebengine_renderer_manifest") {
  sources = [
    "qtwebengine_renderer_manifest.cc",
    "qtwebengine_renderer_manifest.h",
  ]

  deps = [
    "//base",
    "//components/spellcheck:buildflags",
    "//services/service_manager/public/cpp",
  ]
  if (enable_spellcheck) {
    deps += [ "//components/spellcheck/common:interfaces" ]
  }
}


source_set("service_manifests") {
  public_deps = [
    ":qtwebengine_content_browser_overlay_manifest",
    ":qtwebengine_content_renderer_overlay_manifest",
    ":qtwebengine_packaged_service_manifests",
    ":qtwebengine_renderer_manifest",
  ]
}

group("resources") {
  deps = [
    ":qt_webengine_resources",
  ]
}
