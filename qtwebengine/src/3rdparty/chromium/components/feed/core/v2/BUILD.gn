# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("feed_core_v2") {
  sources = [
    "enums.cc",
    "enums.h",
    "feed_network.cc",
    "feed_network.h",
    "feed_network_impl.cc",
    "feed_network_impl.h",
    "feed_store.cc",
    "feed_store.h",
    "feed_stream.cc",
    "feed_stream.h",
    "prefs.cc",
    "prefs.h",
    "proto_util.cc",
    "proto_util.h",
    "public/feed_service.cc",
    "public/feed_service.h",
    "public/feed_stream_api.h",
    "refresh_task_scheduler.h",
    "request_throttler.cc",
    "request_throttler.h",
    "scheduling.cc",
    "scheduling.h",
    "stream_event_metrics.cc",
    "stream_event_metrics.h",
    "stream_model.cc",
    "stream_model.h",
    "stream_model/ephemeral_change.cc",
    "stream_model/ephemeral_change.h",
    "stream_model/feature_tree.cc",
    "stream_model/feature_tree.h",
    "stream_model_update_request.cc",
    "stream_model_update_request.h",
    "tasks/load_stream_from_store_task.cc",
    "tasks/load_stream_from_store_task.h",
    "tasks/load_stream_task.cc",
    "tasks/load_stream_task.h",
    "tasks/wait_for_store_initialize_task.cc",
    "tasks/wait_for_store_initialize_task.h",
  ]
  deps = [
    "//components/feed/core:feed_core",
    "//components/feed/core/common:feed_core_common",
    "//components/offline_pages/task:task",
    "//components/prefs",
    "//components/signin/public/identity_manager",
    "//components/variations",
    "//components/variations/net",
    "//components/web_resource:web_resource",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/zlib/google:compression_utils",
  ]

  public_deps = [
    "//base",
    "//components/feed/core/common:feed_core_common",
    "//components/feed/core/proto:proto_v2",
  ]
}

source_set("core_unit_tests") {
  testonly = true
  sources = [
    "feed_network_impl_unittest.cc",
    "feed_store_unittest.cc",
    "feed_stream_unittest.cc",
    "request_throttler_unittest.cc",
    "stream_model_unittest.cc",
    "stream_model_update_request_unittest.cc",
    "test/callback_receiver.h",
    "test/callback_receiver.h",
    "test/proto_printer.cc",
    "test/proto_printer.h",
    "test/stream_builder.cc",
    "test/stream_builder.h",
  ]

  deps = [
    ":feed_core_v2",
    ":unit_tests_bundle_data",
    "//base",
    "//base/test:test_support",
    "//components/feed/core:feed_core",
    "//components/feed/core/common:feed_core_common",
    "//components/leveldb_proto:test_support",
    "//components/prefs:test_support",
    "//components/signin/public/identity_manager",
    "//components/signin/public/identity_manager:test_support",
    "//net:test_support",
    "//services/network:test_support",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//testing/gtest",
    "//third_party/zlib/google:compression_utils",
  ]
}

bundle_data("unit_tests_bundle_data") {
  visibility = [ ":core_unit_tests" ]
  testonly = true
  sources = [ "//components/test/data/feed/response.binarypb" ]
  outputs = [ "{{bundle_resources_dir}}/" +
              "{{source_root_relative_dir}}/{{source_file_part}}" ]
}
