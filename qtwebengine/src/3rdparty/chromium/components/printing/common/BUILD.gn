# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//printing/buildflags/buildflags.gni")

static_library("common") {
  sources = [
    "cloud_print_cdd_conversion.cc",
    "cloud_print_cdd_conversion.h",
    "print_messages.cc",
    "print_messages.h",
    "printing_param_traits_macros.h",
  ]

  deps = [
    "//base",
    "//components/cloud_devices/common:common",
    "//ipc",
    "//printing",
    "//printing/common:common",
    "//third_party/blink/public:blink_headers",
    "//ui/accessibility",
    "//ui/gfx/geometry",
    "//ui/gfx/ipc",
    "//ui/gfx/ipc/geometry",
    "//ui/gfx/ipc/skia",
  ]
  if (use_qt) {
    sources -= [
      "cloud_print_cdd_conversion.cc",
      "cloud_print_cdd_conversion.h",
    ]
    deps -= [
      "//components/cloud_devices/common:common",
    ]
  }
}

mojom("mojo_interfaces") {
  sources = [ "print.mojom" ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/gfx/geometry/mojom",
  ]

  if (enable_print_preview) {
    enabled_features = [ "enable_print_preview" ]
  }
}
